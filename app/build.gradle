plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jetbrains.kotlin.kapt'
    id "com.gladed.androidgitversion" version "0.4.13"
    id 'com.google.gms.google-services'
}

apply from: "$rootDir/dependencies.gradle"

Properties properties = new Properties()
File propertiesFile = new File('key.properties')
propertiesFile.withInputStream {
    properties.load(it)
}

android {

    androidGitVersion {
        codeFormat = "MNNPPPRR"
    }

    compileSdkVersion 31

    defaultConfig {
        applicationId "com.github.pidsamhai.covid19thailand"
        minSdkVersion 22
        targetSdkVersion 31
        if (androidGitVersion.code() == 0) {
            versionCode 1
        } else  {
          versionCode androidGitVersion.code()
        }
        versionName androidGitVersion.name()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "OWNER", "\"Pidsamhai\""
        buildConfigField "String", "REPO", "\"CoVid19_Thailand\""
    }
    signingConfigs {
        release {
            keyAlias = properties."KEY_ALIAS"
            keyPassword = properties."KEY_PASSWORD"
            storePassword = properties."STORE_PASSWORD"
            storeFile = file("psm_covid_19.jks")
            enableV3Signing = true
            enableV4Signing = true
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
//        android.compileOptions.incremental = false
    }
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = false
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    externalNativeBuild {
        cmake {
            path = "CMakeLists.txt"
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$Versions.compose"
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas")
            arg("room.incremental", "true")
//            arg("room.expandProjection", "true")
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$Versions.core_ktx"
    implementation "androidx.appcompat:appcompat:$Versions.appcompat"

    testImplementation "junit:junit:$Versions.junit"
    androidTestImplementation "androidx.test.ext:junit:$Versions.androidx_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$Versions.expresso_core"

    implementation "com.google.android.material:material:$Versions.material"
    implementation "com.squareup.retrofit2:retrofit:$Versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$Versions.retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$Versions.retrofit"
    implementation("com.squareup.okhttp3:logging-interceptor:$Versions.okhttp")

    implementation "androidx.room:room-ktx:$Versions.room"
    kapt "androidx.room:room-compiler:$Versions.room"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.google.code.gson:gson:$Versions.gson"

    implementation "io.insert-koin:koin-android:$Versions.koin"
    implementation "io.insert-koin:koin-androidx-compose:$Versions.koin"

    implementation "com.jakewharton.timber:timber:$Versions.timber"

    implementation "androidx.compose.ui:ui:$Versions.compose"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling-preview:$Versions.compose"
    debugImplementation "androidx.compose.ui:ui-tooling:$Versions.compose"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$Versions.compose"
    // Material Design
    implementation "androidx.compose.material:material:$Versions.compose"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core:$Versions.compose"
    implementation "androidx.compose.material:material-icons-extended:$Versions.compose"
    // Integration with activities
    implementation "androidx.activity:activity-compose:$Versions.activity_compose"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$Versions.viewmodel_compose"
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata:$Versions.compose"

    implementation "androidx.navigation:navigation-compose:$Versions.navigation_compose"

    implementation "com.github.AAChartModel:AAChartCore-Kotlin:$Versions.aa_chart"

    implementation "com.google.accompanist:accompanist-swiperefresh:$Versions.acc_companist"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$Versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$Versions.lifecycle"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:$Versions.kotlinx_datetime"

    implementation "io.noties.markwon:core:$Versions.markwon"
    implementation"io.noties.markwon:image-coil:$Versions.markwon"
    implementation"io.noties.markwon:linkify:$Versions.markwon"

    implementation "com.navelplace:jsemver:$Versions.jsemver"

    implementation platform("com.google.firebase:firebase-bom:$Versions.firebase_bom")
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    /* Glance Widget */
    implementation 'androidx.glance:glance:1.0.0-alpha01'
    implementation 'androidx.glance:glance-appwidget:1.0.0-alpha01'
    implementation 'androidx.glance:glance-appwidget-proto:1.0.0-alpha01'

    /* Work Manager */
    implementation "androidx.work:work-runtime-ktx:$Versions.work_manager"

    /**
     * Fix JDK 11 Compile Error
     */
    compileOnly("javax.annotation:javax.annotation-api:1.3.2")
    compileOnly("com.github.pengrad:jdk9-deps:22e725c32e")
}